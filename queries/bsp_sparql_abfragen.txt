https://int.lindas.admin.ch/sparql/

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rico: <https://www.ica.org/standards/RiC/ontology#>
PREFIX archiving: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/archiving#>
PREFIX certainty-value: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/certainty-value#>
PREFIX person: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/person#>
PREFIX marriage: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/marriage#>
PREFIX date: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/date#>
PREFIX organisation: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/organisation#>
PREFIX place: <https://github.com/stazh/sw-ehedaten/tree/main/ontology/place#>

# alle Bände und deren Einträge. 
SELECT * WHERE {
   ?record a archiving:Record.
   ?record archiving:recordHasTitleLiteral ?title.
   ?record archiving:recordHasDateOfOriginLiteral ?date.
   ?record archiving:recordHasProvenance ?kirchgemeinde.
   ?kirchgemeinde organisation:parishHasNameLiteral ?kirchgemeindeName.
   ?record archiving:recordHasRecordPart ?recordPart.
} LIMIT 100

# Alle Vornamen der Frauen
SELECT * WHERE {
  ?woman a person:Woman .
  ?woman person:personHasFirstNameLiteral ?vornameFrau .
} ORDER BY (?vornameFrau)



# Zählen der Frauenvornamen-Vorkommen und der Grösse nach ordnen
SELECT * WHERE{
SELECT ?vornameFrau (COUNT(?vornameFrau) as ?anzahl) WHERE {
  	?entry marriage:marriageEntryRegistersWoman ?woman.
    ?woman person:personHasFirstNameLiteral ?vornameFrau .
	} GROUP BY ?vornameFrau
} ORDER BY DESC (?anzahl)

# Entwicklung Anna ab 1700
SELECT * WHERE {
  ?entry marriage:marriageEntryRegistersWoman ?woman.
  ?woman person:personHasFirstNameLiteral "Anna" .
  ?entry marriage:marriageEntryHasDatingOnDate ?datum .
  {?datum date:gregorianDating ?gregOderJulDatum} UNION {?datum date:julianDating ?gregOderJulDatum}.
  BIND (xsd:integer(STRBEFORE(STR(?gregOderJulDatum), "-" )) AS ?jahr )
} ORDER BY ?jahr


# Heiratswochentage Entwicklung
SELECT * WHERE{
?me marriage:marriageEntryDocumentsWeddingWithCertaintyValue certainty-value:Likely.
?me marriage:marriageEntryHasDatingOnDate ?date.
?date date:dateHasWeekday ?weekday.
?weekday rdfs:label ?weekdayLabel.
filter (lang(?weekdayLabel) = 'de')
?date date:dateIsInGregorianYear ?year.
}

# Heiratswochentage Entwicklung Zürich

SELECT * WHERE{
?me marriage:marriageEntryDocumentsWeddingWithCertaintyValue certainty-value:Likely.
?me marriage:marriageEntryHasDatingOnDate ?date.
?me marriage:marriageEntryIsInParishBook ?pb.
?pb marriage:parishBookIsKeptByParish ?kirchgemeinde.
?kirchgemeinde organisation:parishHasSeatAtPlace <https://github.com/stazh/sw-ehedaten/tree/main/data#PlaceName_Zuerich>.
?date date:dateHasWeekday ?weekday.
?weekday rdfs:label ?weekdayLabel.
filter (lang(?weekdayLabel) = 'de')
?date date:dateIsInGregorianYear ?year.
}
